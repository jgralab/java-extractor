module java/statement/Statements

imports
	java/member/Members %% FieldDeclaration ParamDeclaration VariableDeclaration
	java/type/ClassifierDeclarations %% ClassDeclaration EnumDeclaration
	java/expression/Expressions %% Expression
	java/Identifiers %% Id

symbol tables
	name2Label<Label> %% may not be pushed or popped!!

exports
  sorts Statement Block
  sorts SwitchGroup SwitchLabel CatchClause OpenStatement ClosedStatement

  context-free syntax  
    %% statements
    rule OpenStatement -> Statement #$ = $0;#
	rule ClosedStatement -> Statement #$ = $0;#
  
    %% statements without trailing substatements
	rule Block		-> ClosedStatement #$ = $0;#
	rule #$ = Block();# "{" Statement #ContainsBlockStatement($, $0);# * "}" -> Block
	
	rule ";"        -> ClosedStatement #$ = {return utilities.getEmptyStatement(currentElement.getPosition());};#
	
	rule #
			$isLocalVariable = {return isLocalVariable;};
			{isLocalVariable = true;}
		#
		FieldDeclaration -> ClosedStatement #
			{isLocalVariable = (Boolean) #$isLocalVariable#;}
			$ = $0;
		#
	rule ClassDeclaration -> ClosedStatement #
		$ = TypeDefinitionStatement();
		ContainsDefinedType($, $0);
	#
	rule EnumDeclaration -> ClosedStatement #
		$ = TypeDefinitionStatement();
		ContainsDefinedType($, $0);
	#
	
	rule Expression ";"   -> ClosedStatement {avoid} #$ = $0;#
	
	rule "switch" "(" Expression ")" "{" SwitchGroup* SwitchLabel* "}" -> ClosedStatement
    rule SwitchLabel+ Statement+ -> SwitchGroup
    rule "case" Expression ":" -> SwitchLabel
    rule "default"   ":" -> SwitchLabel
	
	rule "do" Statement "while" "(" Expression ")" ";" -> ClosedStatement
	
	rule #$ = Break();# "break"    Id #BreaksTo($, name2Label.use(lexem($0)));# ? ";"   -> ClosedStatement
	rule #$ = Continue();# "continue" Id #ContinuesAt($, name2Label.use(lexem($0)));# ? ";"   -> ClosedStatement
	rule "return"   Expression? ";" -> ClosedStatement
	rule "throw"    Expression  ";" -> ClosedStatement
	rule "synchronized" "(" Expression ")" Block -> ClosedStatement
	
    rule #$ = Assert();# 
		"assert" Expression #ContainsAssertCondition($, $1);#
		(
			":"
			Expression #ContainsMessage($, $1);#
		)?
		";" -> ClosedStatement
	
    rule "try" Block CatchClause* ("finally" Block)? -> ClosedStatement
    rule "catch" "(" ParamDeclaration ")" Block -> CatchClause
	
    rule "if" "(" Expression ")" ClosedStatement "else" ClosedStatement -> ClosedStatement
	
	rule Id ":" # 
			$identifier = name2Identifier.use(lexem($0));
			{
				if (#$identifier# == null) {#
					$identifier = Identifier();
					$identifier.name = lexem($0);
					name2Identifier.declare(lexem($0), $identifier);
				#} else {
					getPositionsMap().put((Vertex) #$identifier#, currentElement.getChild(0).getPosition());
				}
			}
			$ = Label();
			HasLabelName($, $identifier);
			name2Label.declare(lexem($0), $);
		#
		ClosedStatement -> ClosedStatement #
			ContainsLabelStatement($, $2);
			name2Label.getTop().getMap().remove(lexem($0));
		#
	
	rule "while" "(" Expression ")" ClosedStatement -> ClosedStatement
	
	rule "for" "(" (VariableDeclaration | {Expression ","}*) ";" Expression? ";" {Expression ","}* ")" ClosedStatement -> ClosedStatement

    rule "for" "(" ParamDeclaration ":" Expression ")" ClosedStatement -> ClosedStatement
	
	%% statements with trailing substatements
	
	rule Id ":" # 
			$identifier = name2Identifier.use(lexem($0));
			{
				if (#$identifier# == null) {#
					$identifier = Identifier();
					$identifier.name = lexem($0);
					name2Identifier.declare(lexem($0), $identifier);
				#} else {
					getPositionsMap().put((Vertex) #$identifier#, currentElement.getChild(0).getPosition());
				}
			}
			$ = Label();
			HasLabelName($, $identifier);
			name2Label.declare(lexem($0), $);
		#
		OpenStatement -> OpenStatement #
			ContainsLabelStatement($, $2);
			name2Label.getTop().getMap().remove(lexem($0));
		#
	
	rule "while" "(" Expression ")" OpenStatement -> OpenStatement
	
	rule "for" "(" (VariableDeclaration | {Expression ","}*) ";" Expression? ";" {Expression ","}* ")" OpenStatement -> OpenStatement

    rule "for" "(" ParamDeclaration ":" Expression ")" OpenStatement -> OpenStatement
	
    rule "if" "(" Expression ")" Statement -> OpenStatement
    rule "if" "(" Expression ")" ClosedStatement "else" OpenStatement -> OpenStatement